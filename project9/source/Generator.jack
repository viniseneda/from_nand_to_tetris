class Generator 
{
	field Array last;
	field String seed;
	field int size;
	
	constructor Generator new(String s, int si){
	let last = Array.new(si);
	let size = si;
	let last[size/2] = 1;
	let seed = s;
	return this;
	}
	
	method Array get_last() {return last;}

	method void reset(String se)
	{
		var int i;
		let seed = se;
		
		let i = 0;
		while (i < size)
		{
			let last[i] = 0;
			let i = i + 1;
		}
		let last[size/2] = 1;
		return;
	}
	
	//applies the rules to each of the possible combination of 3 units
	method void process()
	{
		var Array new;
		var int x;
		var int i;

		let x = 0;
		let new = Array.new(size);
		let i = 0;
		while ((x + 2) < size) {
			if (last[x] = 1)
			{
				if (last[x+1] = 1)
				{
					if (last[x+2] = 1) 
					{
						let new[x+1] = seed.charAt(0) - 48;
					}
					else
					{
						let new[x+1] = seed.charAt(1) - 48;
					 }
				}
				else
				{
					if (last[x+2] = 1) 
					{
						let new[x+1] = seed.charAt(2) - 48;
					}
					else
					{
						let new[x+1] = seed.charAt(3) - 48;
					 }
				}
			}
			else
			{
				if (last[x+1] = 1)
				{
					if (last[x+2] = 1) 
					{
						let new[x+1] = seed.charAt(4) - 48;
					}
					else
					{
						let new[x+1] = seed.charAt(5) - 48;
					 }
				}
				else
				{
					if (last[x+2] = 1) 
					{
						let new[x+1] = seed.charAt(6) - 48;
					}
					else
					{
						let new[x+1] = seed.charAt(7) - 48;
					 }
				}
			}
			let x = x + 1;
		}
		let x = 0;
		while (x < size)
		{
			let last[x] = new[x];
			let x = x + 1;
		}
		do new.dispose();
		return;
	}
}
